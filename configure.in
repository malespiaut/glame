dnl Process this file with autoconf to produce a configure script.
AC_INIT(configure.in)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(glame, 0.023)


dnl one switch to configure debugging stuff - sane default for now
AC_ARG_ENABLE(debug, [  --enable-debug          lots of debugging code ], debug=$enableval, debug=1)
if test "$debug" = "no"; then
	debug=0
elif test "$debug" = "yes"; then
	debug=2
fi
if test $debug -eq 2; then
	AC_DEFINE(DEBUG)
elif test $debug -eq 0; then
	AC_DEFINE(NDEBUG)
fi

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AM_PROG_LIBTOOL

if test $GCC = "yes"; then
	AC_DEFINE(HAVE_GCC)
	if test $debug -lt 2; then
		CFLAGS="$CFLAGS -Wall"
	else
		CFLAGS="-Wall -g -O0"
	fi
fi

dnl
dnl Checks for libraries.
dnl use GLAME_LIBS & GLAME_CFLAGS

GLAME_LIBS=""
GLAME_CFLAGS=""



dnl GTK checks
dnl 
AM_PATH_GTK(1.2.0, [have_gtk=yes],
  [AC_MSG_RESULT(Test for GTK failed. See the file 'INSTALL' for help.)
  have_gtk=no])
dnl 
changequote(,)dnl
dnl 
dnl Evil stuff to extract GLIB stuff from gtk-config output
dnl (we want to make sure it matches with the gtk we're using)
GLIB_CFLAGS=`echo $GTK_CFLAGS | sed 's/^.*\(-I[^ ]*glib[^ ]*  *-I[^ ]*\).*$/\1/'`
GLIB_LDFLAGS=`echo $GTK_LIBS | sed -e 's/^.*-lgdk[^ ]* *\(-L[^ ]*\).*$/\1/' -e 's/^.* -lgdk[^ ]* .*$//'`
if test -z "$GLIB_LDFLAGS" ; then
GLIB_LDFLAGS=`echo $GTK_LIBS | sed 's/^ *\(-L[^ ]*\) .*$/\1/'`
fi
GLIB_LIBS="$GLIB_LDFLAGS `echo $GTK_LIBS | sed 's/^.*\(-lglib[^ ]*\).*$/\1/'`"
dnl 
changequote([,])dnl

AM_CONDITIONAL(HAVE_GTK, test x$have_gtk=xyes)

dnl gnome check
have_gnome = "no"
GNOME_INIT_HOOK(have_gnome = "yes",,)

AM_CONDITIONAL(HAVE_GNOME, test x$have_gnome = xyes)

dnl
dnl Checks for various audio stuff.
dnl

dnl check for libaudiofile
AC_CHECK_LIB(audiofile, afCloseFile, laf=yes, laf=no)
AC_CHECK_HEADER(audiofile.h,, laf=no)
if test $laf = yes; then
	GLAME_LIBS="$GLAME_LIBS -laudiofile"
	AC_DEFINE(HAVE_AUDIOFILE)
fi

dnl check for libsndfile - FIXME
lsf=no
if test $lsf = yes; then
	GLAME_LIBS="$GLAME_LIBS -lsndfile"
	AC_DEFINE(HAVE_SNDFILE)
fi

dnl check for esd
AM_PATH_ESD(0.2.0, esd=yes, esd=no)
if test $esd = yes; then
	GLAME_LIBS="$GLAME_LIBS $ESD_LIBS"
	GLAME_CFLAGS="$GLAME_CFLAGS $ESD_CFLAGS"
	AC_DEFINE(HAVE_ESD)
fi

dnl check for native IRIX audio
AC_CHECK_LIB(audio, alWriteBuffers, la=yes, la=no)
AC_CHECK_HEADER(dmedia/audio.h,, la=no)
if test $la = yes; then
	GLAME_LIBS="$GLAME_LIBS -laudio"
	AC_DEFINE(HAVE_SGIAUDIO)
fi


dnl check for electric fence
efence=""
if test $debug -eq 2; then
	AC_CHECK_LIB(efence, Page_AllowAccess, efence="-lefence")
fi
DEBUG_LIBS="$efence"

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_DIRENT
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h sys/file.h unistd.h)



dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_INLINE
AC_TYPE_OFF_T
AC_STRUCT_ST_BLOCKS
AC_TYPE_SIGNAL
AC_TYPE_PID_T

dnl Checks for library functions.
AC_FUNC_MMAP

dnl
AM_CONDITIONAL(STATICLIBS, test x$enable_static = xyes)

AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)
AC_SUBST(GLAME_LIBS)
AC_SUBST(GLAME_CFLAGS)

AC_OUTPUT(Makefile src/Makefile src/filter/Makefile src/libfixp/Makefile src/swapfile/Makefile src/rw/Makefile src/hash/Makefile src/channel/Makefile src/include/Makefile doc/Makefile src/gui/Makefile)
case "$host_cpu" in
i?86)
	AC_DEFINE(CPU_X86);;
mips*)
	AC_DEFINE(CPU_MIPS);;
esac
 

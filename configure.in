dnl Process this file with autoconf to produce a configure script.
AC_INIT(configure.in)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(glame, 0.1.28)


dnl one switch to configure debugging stuff - sane default for now
AC_ARG_ENABLE(debug, [  --enable-debug          lots of debugging code ], debug=$enableval, debug=1)
if test "$debug" = "no"; then
	debug=0
elif test "$debug" = "yes"; then
	debug=2
fi
if test $debug -eq 2; then
	AC_DEFINE(DEBUG)
elif test $debug -eq 0; then
	AC_DEFINE(NDEBUG)
fi

dnl switch to disable gui
AC_ARG_ENABLE(gui, [  --enable-gui            GUI - needs recent GTK/GNOME ], gui=$enableval, gui=yes)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AM_PROG_LIBTOOL

if test $GCC = "yes"; then
	AC_DEFINE(HAVE_GCC)
	if test $debug -eq 0; then
		CFLAGS="-O3 -fomit-frame-pointer -ffast-math"
	elif test $debug -eq 1; then
		CFLAGS="$CFLAGS -Wall"
	else
		CFLAGS="-Wall -g -O0"
	fi
else
  AC_MSG_WARN([Compilers other than gcc are not supported])
fi

dnl
dnl Checks for libraries.
dnl use GLAME_LIBS & GLAME_CFLAGS
dnl and GLAME_DLIBS & GLAME_DCFLAGS
dnl
AC_CHECK_POSIX_RTSIG

if test -z "$HAVE_POSIX_RTSIG"; then
	AC_MSG_ERROR([Libc does not support POSIX realtime signals])
fi


dnl
dnl GUI stuff
dnl

dnl GTK check
if test x$gui = xyes; then
  AM_PATH_GTK(1.2.0, [have_gtk=yes],
    [AC_MSG_RESULT(Test for GTK failed. See the file 'INSTALL' for help.)
    have_gtk=no])
else
  AC_MSG_RESULT([GUI was disabled.])
fi
AM_CONDITIONAL(HAVE_GTK, test x$have_gtk=xyes)

dnl GNOME check
if test x$have_gtk = xyes; then
  GNOME_INIT_HOOK([have_gnome=yes],,)
fi
AM_CONDITIONAL(HAVE_GNOME, test x$have_gnome = xyes)

GNOME_CHECK_GUILE()


dnl
dnl Checks for various audio stuff.
dnl

dnl check for libaudiofile
AC_CHECK_LIB(audiofile, afCloseFile, laf=yes, laf=no)
AC_CHECK_HEADER(audiofile.h,, laf=no)
if test $laf = yes; then
	GLAME_AUDIO_LIBS="$GLAME_AUDIO_LIBS -laudiofile"
	AC_DEFINE(HAVE_AUDIOFILE)
fi

dnl check for OSS
dnl FIXME: Currently this check is Linux specific.
dnl OSS however is not. BSD keeps the header file
dnl in sys/, other Unices in machine/
AC_CHECK_HEADER(linux/soundcard.h, oss=yes, oss=no)
if test $oss = yes; then
	AC_DEFINE(HAVE_OSS)
fi

dnl check for libsndfile - FIXME
lsf=no
if test $lsf = yes; then
	GLAME_AUDIO_LIBS="$GLAME_AUDIO_LIBS -lsndfile"
	AC_DEFINE(HAVE_SNDFILE)
fi

dnl check for esd
AM_PATH_ESD(0.2.0, esd=yes, esd=no)
if test $esd = yes; then
	GLAME_AUDIO_LIBS="$GLAME_AUDIO_LIBS $ESD_LIBS"
	GLAME_AUDIO_CFLAGS="$GLAME_AUDIO_CFLAGS $ESD_CFLAGS"
	AC_DEFINE(HAVE_ESD)
fi

dnl check for native IRIX audio
AC_CHECK_LIB(audio, alWriteBuffers, la=yes, la=no)
AC_CHECK_HEADER(dmedia/audio.h,, la=no)
if test $la = yes; then
	GLAME_AUDIO_LIBS="$GLAME_AUDIO_LIBS -laudio"
	AC_DEFINE(HAVE_SGIAUDIO)
fi

dnl check for ALSA
AM_PATH_ALSA(0.5.0)
if test $ALSA_LIBS; then
	GLAME_LIBS="$GLAME_LIBS $ALSA_LIBS"
	GLAME_CFLAGS="$GLAME_CFLAGS $ALSA_CFLAGS"
	AC_DEFINE(HAVE_ALSA)
fi


dnl
dnl extra debugging stuff
dnl

dnl check for electric fence - temp. disabled due to efence bugs(!?)
if test $debug -eq 2; then
	efence=""
	AC_CHECK_LIB(efence, Page_AllowAccess, efence="-lefence")
dnl	GLAME_DLIBS="$GLAME_DLIBS $efence"
fi



dnl
dnl UNIX flavours compatibility stuff
dnl

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_DIRENT
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h sys/file.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_INLINE
AC_TYPE_OFF_T
AC_STRUCT_ST_BLOCKS
AC_TYPE_SIGNAL
AC_TYPE_PID_T

dnl Checks for library functions.
AC_FUNC_MMAP



dnl
dnl Checks for asm code support
dnl

dnl special asm code hooks for cpu
case "$host_cpu" in
i?86)
	AC_DEFINE(CPU_X86);;
mips*)
	AC_DEFINE(CPU_MIPS);;
esac


dnl
AM_CONDITIONAL(STATICLIBS, test x$enable_static = xyes)

AC_SUBST(GLAME_LIBS)
AC_SUBST(GLAME_CFLAGS)
AC_SUBST(GLAME_DLIBS)
AC_SUBST(GLAME_DCFLAGS)
AC_SUBST(GLAME_AUDIO_LIBS)
AC_SUBST(GLAME_AUDIO_CFLAGS)

AC_OUTPUT(Makefile doc/Makefile src/Makefile src/filter/Makefile src/swapfile/Makefile src/hash/Makefile src/channel/Makefile src/plugin/Makefile src/include/Makefile src/gui/Makefile src/glmath/Makefile src/data/Makefile src/data/pixmaps/Makefile src/glmid/Makefile)


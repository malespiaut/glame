dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(glame, 0.4.0beta)
AM_CONFIG_HEADER(config.h)

dnl Pick up additional macros from the macros directory
AM_ACLOCAL_INCLUDE(macros)


dnl
dnl Switch to configure debugging stuff - sane default for now.
dnl

AC_ARG_ENABLE(debug,
[  --enable-debug          include lots of debugging code 
  --disable-debug         compile for maximum optimization], debug=$enableval, debug=1)
if test "$debug" = "no"; then
	debug=0
elif test "$debug" = "yes"; then
	debug=2
fi
if test $debug -eq 2; then
	AC_DEFINE(DEBUG)
elif test $debug -eq 0; then
	AC_DEFINE(NDEBUG)
fi


dnl
dnl Checks for programs.
dnl

dnl Compiling without gcc is not supported but may succeed.
AC_PROG_CC
if test "$GCC" = "yes"; then
	AC_DEFINE(HAVE_GCC)
	if test $debug -eq 0; then
		CFLAGS="-Wall -O3 -fomit-frame-pointer -ffast-math -fstrict-aliasing"
	elif test $debug -eq 1; then
		CFLAGS="$CFLAGS -Wall"
	else
		CFLAGS="-Wall -g -O0"
	fi
else
  AC_MSG_WARN([Compilers other than gcc are not supported])
fi

AC_PROG_INSTALL
AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL



dnl FIXME: The next two checks break cross-compiling.
dnl        Unfortunately run-time checks would mean performance impacts or are
dnl        simply not an option. [dk]

dnl Check endianness
AC_C_BIGENDIAN
AC_CHECK_HEADER(byteswap.h,AC_DEFINE(HAVE_BYTESWAP_H),)

dnl Check width of basic types
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(long long)


dnl
dnl Switch to make SAMPLE a double - float is default.
dnl

AC_ARG_ENABLE(double, [  --enable-double         use doubles instead of floats (not recommended)], sample=$enableval, sample=float)
if test "$sample" = "no"; then
	sample=float
elif test "$sample" = "yes"; then
	sample=double
	AC_MSG_WARN([Using double for SAMPLE which is not supported])
fi
if test "$sample" = "float"; then
	AC_DEFINE(SAMPLE_FLOAT)
fi
AC_DEFINE_UNQUOTED(SAMPLE, $sample)


dnl
dnl Checks for libraries.
dnl use GLAME_LIBS & GLAME_CFLAGS
dnl and GLAME_DLIBS & GLAME_DCFLAGS
dnl



dnl
dnl First check all _required_ stuff
dnl


dnl
dnl pthread lib - GLAME is threaded everywhere, so global
dnl LIBS and CFLAGS use is necessary
dnl

ACX_PTHREAD([
LIBS="$PTHREAD_LIBS"
CFLAGS="$CFLAGS $PTHREAD_CFLAGS"
CC="$PTHREAD_CC"
AC_DEFINE(USE_PTHREADS)
], [
AC_ERROR(You need pthreads to run glame!)
])

dnl
dnl GUILE check, we absolutely need it, so fail if its not there
dnl the check has AM_CONDITIONAL(GUILE, ...) and defines HAVE_GUILE
dnl

GNOME_CHECK_GUILE("fail")

dnl
dnl We also need libxml
dnl

AMG_PATH_XML(1.8.0, , [
AC_ERROR(You need libxml to run glame!)
])



dnl
dnl The rest is optional stuff
dnl


dnl
dnl GUI stuff - require GNOME for anything
dnl

dnl switch to disable gui
AC_ARG_ENABLE(gui, [  --disable-gui           GUI - needs recent GTK/GNOME ], gui=$enableval, gui=yes)

dnl GNOME check
if test x$gui = xyes; then
  GNOME_INIT_HOOK([have_gnome=yes],,)
else
  AC_MSG_RESULT([GUI was disabled.])
fi
AM_CONDITIONAL(HAVE_GNOME, test x$have_gnome = xyes)




dnl
dnl check for single/double precision fftw
dnl
if test "$sample" = "float"; then
	AC_CHECK_LIB(sfftw, fftw2d_create_plan, fftw=yes, fftw=no)
	AC_CHECK_HEADER(sfftw.h,, fftw=no)
	if test $fftw = yes; then
		GLAME_FFT_LIBS="$GLAME_FFT_LIBS -lsfftw -lsrfftw"
		AC_DEFINE(HAVE_FFTW)
	fi
elif test "$sample" = "double"; then
	AC_CHECK_LIB(fftw, fftw2d_create_plan, fftw=yes, fftw=no)
	AC_CHECK_HEADER(fftw.h,, fftw=no)
	if test $fftw = yes; then
		GLAME_FFT_LIBS="$GLAME_FFT_LIBS -lfftw -lrfftw"
		AC_DEFINE(HAVE_FFTW)
	fi
fi
AM_CONDITIONAL(HAVE_FFTW, test x$fftw = xyes)


dnl
dnl Checks for various audio stuff.
dnl

dnl check for libaudiofile, use audiofile-config, if available
AC_CHECK_PROG(afconfig, audiofile-config, yes, no)
if test $afconfig = no; then
	AC_CHECK_LIB(audiofile, afCloseFile, laf=yes, laf=no)
	AC_CHECK_HEADER(audiofile.h,, laf=no)
	if test $laf = yes; then
		GLAME_AUDIO_LIBS="$GLAME_AUDIO_LIBS -laudiofile"
		AC_DEFINE(HAVE_AUDIOFILE)
	fi
else
	GLAME_AUDIO_LIBS="$GLAME_AUDIO_LIBS `audiofile-config --libs`"
	GLAME_AUDIO_CFLAGS="$GLAME_AUDIO_CFLAGS `audiofile-config --cflags`"
	AC_DEFINE(HAVE_AUDIOFILE)
fi

dnl check for OSS
dnl BSD keeps the header file
dnl in sys/, Linux in linux/, 
dnl other Unices in machine/
AC_CHECK_HEADER(linux/soundcard.h, oss=yes, oss=no)
if test $oss = yes; then
	AC_DEFINE(HAVE_OSS)
	AC_DEFINE(HAVE_OSS_LINUX)
else
	AC_CHECK_HEADER(sys/soundcard.h, oss=yes, oss=no)
	if test $oss = yes; then
		AC_DEFINE(HAVE_OSS)
		AC_DEFINE(HAVE_OSS_SYS)
	else 
		AC_CHECK_HEADER(machine/soundcard.h, oss=yes, oss=no)
		if test $oss = yes; then
			AC_DEFINE(HAVE_OSS)
			AC_DEFINE(HAVE_OSS_MACHINE)
		fi
	fi
fi

dnl check for libsndfile - FIXME
lsf=no
if test $lsf = yes; then
	GLAME_AUDIO_LIBS="$GLAME_AUDIO_LIBS -lsndfile"
	AC_DEFINE(HAVE_SNDFILE)
fi

dnl check for lame - richi FIX it, if you don't like it :)
AC_CHECK_LIB(mp3lame, lame_decode, lame=yes, lame=no)
AC_CHECK_HEADER(lame.h,,lame=no)
if test $lame = yes; then
	GLAME_AUDIO_LIBS="$GLAME_AUDIO_LIBS -lmp3lame"
	AC_DEFINE(HAVE_LAME)
fi

dnl check for esd
AM_PATH_ESD(0.2.0, esd=yes, esd=no)
if test $esd = yes; then
	GLAME_AUDIO_LIBS="$GLAME_AUDIO_LIBS $ESD_LIBS"
	GLAME_AUDIO_CFLAGS="$GLAME_AUDIO_CFLAGS $ESD_CFLAGS"
	AC_DEFINE(HAVE_ESD)
fi

dnl check for native IRIX audio
AC_CHECK_LIB(audio, alWriteBuffers, la=yes, la=no)
AC_CHECK_HEADER(dmedia/audio.h,, la=no)
if test $la = yes; then
	GLAME_AUDIO_LIBS="$GLAME_AUDIO_LIBS -laudio"
	AC_DEFINE(HAVE_SGIAUDIO)
fi

dnl check for ALSA
AM_PATH_ALSA(0.5.0, alsa="yes", alsa="no")
if test "x$alsa" = xyes; then
	GLAME_AUDIO_LIBS="$GLAME_AUDIO_LIBS $ALSA_LIBS"
	GLAME_AUDIO_CFLAGS="$GLAME_AUDIO_CFLAGS $ALSA_CFLAGS"
	AC_DEFINE(HAVE_ALSA)
fi


dnl
dnl extra debugging stuff
dnl

dnl check for electric fence - temp. disabled due to efence bugs(!?)
if test $debug -eq 2; then
	efence=""
	AC_CHECK_LIB(efence, Page_AllowAccess, efence="-lefence")
dnl	GLAME_DLIBS="$GLAME_DLIBS $efence"
fi



dnl
dnl UNIX flavours compatibility stuff
dnl

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_DIRENT
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h sys/file.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_INLINE
AC_TYPE_OFF_T
AC_STRUCT_ST_BLOCKS
AC_TYPE_SIGNAL
AC_TYPE_PID_T


dnl Check for broken SYSV semaphores
ACG_CHECK_SEMUN
ACG_CHECK_SEMCTL

dnl Check for POSIX semaphores
ACG_CHECK_POSIX_SEMAPHORES

dnl Checks for library functions.
AC_FUNC_MMAP
AC_FUNC_MADVISE
AC_FUNC_ALLOCA


dnl special code hooks for os
case "$host_os" in
*linux*)
	AC_DEFINE(OS_LINUX);;
*bsd*)
	AC_DEFINE(OS_BSD);;
*irix*)
	AC_DEFINE(OS_IRIX);;
esac

dnl
dnl Checks for asm code support
dnl


dnl
dnl Disable fast assembler routines, use slower C code instead.
dnl

AC_ARG_ENABLE(asm,
[  --disable-asm           disable fast assembler routines ], asm=$enableval, asm=yes)

dnl special asm code hooks for cpu -- require GCC.
if test "$asm" = "yes" -a "x$GCC" = "xyes"; then
	case "$host_cpu" in
	i?86)
		AC_DEFINE(CPU_X86);;
	mips*)
		AC_DEFINE(CPU_MIPS);;
	powerpc)
		AC_DEFINE(CPU_PPC);;
	esac
fi



dnl
AM_CONDITIONAL(STATICLIBS, test x$enable_static = xyes)

AC_SUBST(GLAME_LIBS)
AC_SUBST(GLAME_CFLAGS)
AC_SUBST(GLAME_DLIBS)
AC_SUBST(GLAME_DCFLAGS)
AC_SUBST(GLAME_AUDIO_LIBS)
AC_SUBST(GLAME_AUDIO_CFLAGS)
AC_SUBST(GLAME_FFT_LIBS)

AC_OUTPUT(Makefile macros/Makefile doc/Makefile data/Makefile data/pixmaps/Makefile src/Makefile src/filter/Makefile src/swapfile/Makefile src/hash/Makefile src/plugins/Makefile src/include/Makefile src/gui/Makefile src/gui/edit_filter/Makefile src/glmid/Makefile src/gui/libgtkwaveform/Makefile)
